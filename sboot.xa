;----------------------------------------------------------------
;	SBOOT
;   bootloader pro SDrive
;	Robert Petruzela & Radek Sterba
;   Bob!k & Raster, C.P.U., 2008
;----------------------------------------------------------------
;
;- 188 bytes
;
;14.2.2008
;- optimized to use the init and run address in the boot sector
;  (thanks to this, 2 bytes of the return address will not remain hanging in the stack, and in addition, the loader is 2 bytes shorter)
;- cutting off the unnecessary from the "once_and_then_next_block" part
;  (saved 10 bytes)
;- 176 bytes
;- added check of the result of the loading sector, if it is not #1, reload
;- 178 bytes
;- optimization of init address 9 (save 2 bytes)
;- 176 bytes
;- 179 bytes
;
;7.6.2008
;- substitute storeX ($47) for txa,pha a pla,tax
;  (saving one cell in the zero page (at the expense of the stack))
;
;08.04.2021
;- converted to XA (by kbr)
;
buffer	= $700		;the lower byte of the buffer must be 0!

edst = $43	;2
dst = $45	;2
;storeX = $47	;1

sc_nsector = $30a

	;* = buffer+$80-(adr780-bootsector)	;XXX Does not work in XA!
	* = buffer+$80-$21			;now len $21 hardcoded
;
;This first part is in the area below $0780,
;so after the first sector is loaded into the buffer $700-$77f it will be overwritten
;
bootsector		;$72='r' (identification SD'r'ive)
	.byte $72,$02
	.word bootsector,jednou_a_pak_next_block	;once_and_then_next_block
reloc_00 = bootsector+3
reloc_01 = bootsector+5
init
;	jmp *
	lda #<buffer
	sta $304		;can't be replaced by sty $304 because no init at the begining
#print <buffer <> 0		;lower buffers are 0 !
	;a=0
	sta 580			;prevent boot after reset (some programs use a jump through the reset vector (Joust))
	lda #>buffer
reloc_02 = *-1
	sta $305
	lda #$70		;here is sector number $170
	sta $30a		;because it increments by 1 right at the beginning
	lda #$01		;and starts at $171
	sta $30b
	;A=1
;	lda #1
	sta $9			;some games need it (RASTER's invention from Xin1)
;	lda #125		;as if he already had 125 bytes loaded
;	sta index
	rts
	.byte 125		;as if there were 125 bytes in that sector to load the next sector
;
;----------------------
;
adr780
read_next_sector
;	stx storeX
	txa
	pha			;save X
rea1
	jsr $e453
	dey			;is the result of the operation 1 (ok) ? dey decrements to Y=0
	bne rea1		;is Y=0 ? not => again
;	ldx storeX
	pla
	tax			;restore X
	;Y=0			;Y-reg is zero ( used for next sta (ns),y ), not necessary -> replaced by (ns,x)
	sty index
reloc_03 = *-1
readbyte			;reading one byte from buffer and incrementing index pointer
index	= *+1
	lda buffer+125		;overwritting LB, index=$7d
reloc_04 = *-1
;	ldy index
;	lda buffer,y
	inc index
reloc_05 = *-1
lrts
	rts			;used two times, one is for return from get_next_byte
				;second one is for jmp ($02e2)
;--
;
next_block
;
	lda #<lrts		;init address point to rts
	sta $02e2
	lda #>lrts
reloc_06 = *-1
	sta $02e3
;
	ldx #2
nb10
	jsr get_next_byte	;read destination adress
reloc_07 = *-1
	sta edst,x		;edst+2 => dst and in second round edst
neu1
	jsr get_next_byte
reloc_08 = *-1
	sta edst+1,x		;edst+1+2 => dst+1 and in second round edst+1
neu2
	and edst,x		;we are lucky, end address could not be $FFFF
	cmp #$ff
	beq nb10

	dex
	dex
	bpl nb10		;should be X==0;
	bmi nb20		;need to skip one incrementation of dst because block length is edst-dst-1

nb15
	inc dst			;incrementing destination address
	bne nb20
	inc dst+1

nb20
	jsr get_next_byte	;copy next byte from buffer to destination address
reloc_09 = *-1
	ldx #1
	sta (dst-1,x)		;Ughh, I know ;-)
	;x=1
eofb
	lda dst,x
	cmp edst,x
	bne nb15
	dex
	bpl eofb

nb30
	jsr nb40		;everytime jmp on init address, normaly is there pointer on RTS else original init
				;has to be JSR here!!!
reloc_10 = *-1

	jmp next_block		;go for next binary-block
reloc_11 = *-1

;--

nb40
	lda #3
	sta $d20f
	jmp ($02e2)		;jmp on init address

;--

get_next_byte			;return next byte in A-reg

	lda index		;last byte in sector?
reloc_12 = *-1
	cmp buffer+127
reloc_13 = *-1
	bne readbyte		;no - get next byte
;
	inc $30a		;feel with it from the sectors of the file
	bne nxv1		;I follow from $171 very nice
	inc $30b		;he gave a sequential order without gaps or anything
nxv1
	lda buffer+125
reloc_14 = *-1
	ora buffer+126
reloc_15 = *-1
	bne read_next_sector	;goes to the next sector
	;the pointer to the next sector is null
	;so let's finish
	jsr nb40		;due to init (I believe $02e2 still point to RTS)
reloc_16 = *-1
	jmp ($02e0)		;jump on run address
;
;
;the next part exceeds over $800 and after implementation
;it can easily be overwritten with data (if a block is imported from $800...)

jednou_a_pak_next_block		;once_and_then_next_block
jad1
	jsr get_next_byte	;load the first sector and overwrite 0700-077f
reloc_17 = *-1
	sta $02e0
	jsr get_next_byte
reloc_18 = *-1
	sta $02e1
	and $02e0
	cmp #$ff		;are they both FF?
	beq jad1		;yes => then try the other two
;
	lda #0
	sta index
reloc_19 = *-1
	beq next_block		;!
;
;
;	relocation table 20 bytes
;
#if 0
	.byte reloc_00-bootsector
	.byte reloc_01-bootsector
	.byte reloc_02-bootsector
	.byte reloc_03-bootsector
	.byte reloc_04-bootsector
	.byte reloc_05-bootsector
	.byte reloc_06-bootsector
	.byte reloc_07-bootsector
	.byte reloc_08-bootsector
	.byte reloc_09-bootsector
	.byte reloc_10-bootsector
	.byte reloc_11-bootsector
	.byte reloc_12-bootsector
	.byte reloc_13-bootsector
	.byte reloc_14-bootsector
	.byte reloc_15-bootsector
	.byte reloc_16-bootsector
	.byte reloc_17-bootsector
	.byte reloc_18-bootsector
	.byte reloc_19-bootsector
#endif


;sio_cmd
;	.byte $31,$02,$52,$40
;	.word buffer
;	.byte $07,$00,$80,$00
;sc_nsector	*	;,a($ffff)

;	org $4000
;test jmp *
